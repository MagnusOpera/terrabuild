import "_protocol.fss" as Protocol
import "_helpers.fss" as Helpers

/// <summary>Docker extension for image build and publish workflows.</summary>
let append_csv acc value =
  match acc with
  | "" -> value
  | _ -> $"{acc},{value}"

let append_build_arg acc key value =
  Helpers.append_part $"--build-arg {key}=\"{value}\"" acc

let format_platforms platforms =
  let joined = List.fold append_csv "" platforms
  match joined with
  | "" -> ""
  | _ -> $"--platform {joined}"

let format_build_args build_args =
  Map.fold append_build_arg "" build_args

/// <summary>Runs an arbitrary Docker CLI command (action name is forwarded to `docker`).</summary>
/// <param name="args" default="" example="&quot;image prune -f&quot;">Extra arguments for the Docker subcommand.</param>
[<export>] let dispatch (context: Protocol.ActionContext) (args: string option) =
  let command = Helpers.with_args args |> Helpers.append_part context.Command
  [{ Command = "docker"; Arguments = command; ErrorLevel = 0 }]

/// <summary>Builds an image tagged with the Terrabuild hash, and pushes it in CI.</summary>
/// <param name="image" example="&quot;ghcr.io/example/project&quot;">Image repository.</param>
/// <param name="dockerfile" default="Dockerfile" example="&quot;Dockerfile&quot;">Dockerfile path.</param>
/// <param name="platforms" default="host" example="&quot;linux/amd64&quot;">Target platforms.</param>
/// <param name="build_args" default="" example="{ configuration: &quot;Release&quot; }">Build arguments (`--build-arg`).</param>
/// <param name="args" default="" example="&quot;--debug&quot;">Extra `docker build` arguments.</param>
[<export>] let build (context: Protocol.ActionContext) (image: string) (dockerfile: string option) (platforms: string list option) (build_args: string map option) (args: string option) =
  let dockerfile = dockerfile |> Option.defaultValue "Dockerfile"
  let platforms = platforms |> Option.map format_platforms |> Option.defaultValue ""
  let build_args = build_args |> Option.map format_build_args |> Option.defaultValue ""
  let args = args |> Helpers.with_args

  let command =
    ""
    |> Helpers.append_part "build"
    |> Helpers.append_part $"--file {dockerfile}"
    |> Helpers.append_part $"--tag {image}:{context.Hash}"
    |> Helpers.append_part build_args
    |> Helpers.append_part platforms
    |> Helpers.append_part args
    |> Helpers.append_part "."

  let ops = [{ Command = "docker"; Arguments = command; ErrorLevel = 0 }]
  if context.CI then
    ops @ [{ Command = "docker"; Arguments = $"push {image}:{context.Hash}"; ErrorLevel = 0 }]
  else
    ops

/// <summary>Pushes the built image to the registry with a specific tag.</summary>
/// <param name="image" example="&quot;ghcr.io/example/project&quot;">Image repository.</param>
/// <param name="tag" example="&quot;1.2.3-stable&quot;">Target tag.</param>
/// <param name="args" default="" example="&quot;--disable-content-trust&quot;">Extra tagging/push arguments.</param>
[<export>] let push (context: Protocol.ActionContext) (image: string) (tag: string) (args: string option) =
  let args = args |> Helpers.with_args
  let command =
    if context.CI then
      ""
      |> Helpers.append_part "buildx imagetools create"
      |> Helpers.append_part $"-t {image}:{tag}"
      |> Helpers.append_part $"{image}:{context.Hash}"
      |> Helpers.append_part args
    else
      ""
      |> Helpers.append_part "tag"
      |> Helpers.append_part $"{image}:{context.Hash}"
      |> Helpers.append_part $"{image}:{tag}"
      |> Helpers.append_part args
  [{ Command = "docker"; Arguments = command; ErrorLevel = 0 }]

// Exported functions and Terrabuild execution flags.
{
  [nameof dispatch] = [Dispatch; Never]
  [nameof build] = [External]
  [nameof push] = [External]
}
