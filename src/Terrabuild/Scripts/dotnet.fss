import "_protocol.fss" as Protocol
import "_helpers.fss" as Helpers

/// <summary>Provides build, test, pack, and publish helpers for .NET SDK projects (`*.csproj`/`*.fsproj`). Infers project references to wire Terrabuild dependencies and defaults outputs to `bin/`, `obj/`, and build logs.</summary>
let project_extensions = [".csproj"; ".fsproj"; ".vbproj"; ".pssproj"; ".sqlproj"; ".dcproj"]

let first_project_file directory =
  project_extensions
  |> List.collect (fun ext -> Fs.enumerateFiles directory $"*{ext}" |> Option.defaultValue [])
  |> List.tryHead

let project_refs_from project_file =
  let content = Fs.readText project_file |> Option.defaultValue ""
  match Regex.matchGroups "<ProjectReference[^>]*Include=\"([^\"]+)\"" content with
  | Some [value] -> [value]
  | _ -> []

let dependency_dirs refs =
  refs
  |> List.choose Fs.parentDirectory
  |> List.distinct

let with_batch_projects (context: Protocol.ActionContext) create_command =
  match context.Batch with
  | Some batch ->
      batch.ProjectPaths
      |> List.choose first_project_file
      |> List.map create_command
  | _ ->
      [create_command ""]

let command_op arguments =
  { Command = "dotnet"; Arguments = arguments; ErrorLevel = 0 }

/// <summary>Infers project metadata from the nearest SDK project file (dependencies and default outputs).</summary>
/// <param name="ignores" example="[ &quot;**/*.binlog&quot; ]"></param>
/// <param name="outputs" example="[ &quot;bin/&quot; &quot;obj/&quot; &quot;**/*.binlog&quot; ]"></param>
[<export>] let defaults (context: Protocol.ActionContext) =
  let dependencies =
    context.Directory
    |> first_project_file
    |> Option.map (fun project_file -> dependency_dirs (project_refs_from project_file))
    |> Option.defaultValue []

  { Id = None; Outputs = ["bin/"; "obj/"; "**/*.binlog"]; Dependencies = dependencies }

/// <summary>Runs an arbitrary `dotnet` command (action name is forwarded to `dotnet`).</summary>
/// <param name="args" default="" example="&quot;run -- -v&quot;">Arguments appended after the dotnet command.</param>
[<export>] let dispatch (context: Protocol.ActionContext) (args: string option) =
  let command = Helpers.with_args args |> Helpers.append_part context.Command
  [command_op command]

/// <summary>Executes `dotnet tool ...` commands.</summary>
/// <param name="args" default="" example="&quot;install MagnusOpera.OpenApiGen&quot;">Arguments appended after `dotnet tool`.</param>
[<export>] let tool (context: Protocol.ActionContext) (args: string option) =
  let command =
    ""
    |> Helpers.append_part "tool"
    |> Helpers.append_part (Helpers.with_args args)
  [command_op command]

/// <summary>Restores NuGet packages, optionally frozen, forcing evaluation, or batching workspace projects into a temporary solution.</summary>
/// <param name="locked" default="true" example="&quot;true&quot;">Enable locked versions; set `true` to add `--locked-mode`.</param>
/// <param name="evaluate" default="false" example="&quot;true&quot;">Add `--force-evaluate` to refresh resolved packages.</param>
/// <param name="args" default="" example="&quot;--no-dependencies&quot;">Additional arguments for `dotnet restore`.</param>
[<export>] let restore (context: Protocol.ActionContext) (locked: bool option) (evaluate: bool option) (args: string option) =
  let locked = Helpers.map_true "--locked-mode" locked
  let force_evaluate = Helpers.map_true "--force-evaluate" evaluate
  let args = Helpers.with_args args

  let create_command project_file =
    ""
    |> Helpers.append_part "restore"
    |> Helpers.append_part project_file
    |> Helpers.append_part locked
    |> Helpers.append_part force_evaluate
    |> Helpers.append_part args
    |> command_op

  with_batch_projects context create_command

/// <summary>Builds the project or batch solution via `dotnet build`.</summary>
/// <title>Build project.</title>
/// <param name="configuration" default="Debug" example="&quot;Release&quot;">Configuration (defaults to `Debug`).</param>
/// <param name="parallel" default="true" example="1">Max worker processes (`-maxcpucount`).</param>
/// <param name="log" default="false" example="true">Emit a binary log (`-bl`).</param>
/// <param name="restore" default="true" example="&quot;true&quot;">Perform restore (omit to add `--no-restore`).</param>
/// <param name="version" default="" example="&quot;1.2.3&quot;">Set `Version` MSBuild property.</param>
/// <param name="args" default="" example="&quot;--no-incremental&quot;">Additional arguments for `dotnet build`.</param>
[<export>] let build (context: Protocol.ActionContext) (configuration: string option) (parallel: int option) (log: bool option) (restore: bool option) (version: string option) (args: string option) =
  let configuration = Helpers.with_configuration configuration
  let log = Helpers.map_true "-bl" log
  let no_restore = Helpers.map_false "--no-restore" restore
  let maxcpucount = Helpers.map_value (fun value -> $"-maxcpucount:{value}") parallel
  let version = Helpers.map_value (fun value -> $"-p:Version={value}") version
  let args = Helpers.with_args args

  let create_command project_file =
    ""
    |> Helpers.append_part "build"
    |> Helpers.append_part project_file
    |> Helpers.append_part no_restore
    |> Helpers.append_part $"--configuration {configuration}"
    |> Helpers.append_part log
    |> Helpers.append_part maxcpucount
    |> Helpers.append_part version
    |> Helpers.append_part args
    |> command_op

  with_batch_projects context create_command

/// <summary>Packs the project into a NuGet package via `dotnet pack`.</summary>
/// <param name="configuration" default="Debug" example="&quot;Release&quot;">Configuration (defaults to `Debug`).</param>
/// <param name="restore" default="true" example="&quot;true&quot;">Perform restore (omit to add `--no-restore`).</param>
/// <param name="build" default="true" example="&quot;true&quot;">Build before packing (omit to add `--no-build`).</param>
/// <param name="version" default="" example="&quot;1.0.0&quot;">Package version (`/p:Version`).</param>
/// <param name="args" default="" example="&quot;--include-symbols&quot;">Additional arguments for `dotnet pack`.</param>
[<export>] let pack (context: Protocol.ActionContext) (configuration: string option) (version: string option) (restore: bool option) (build: bool option) (args: string option) =
  let configuration = Helpers.with_configuration configuration
  let version = version |> Option.defaultValue "0.0.0"
  let no_restore = Helpers.map_false "--no-restore" restore
  let no_build = Helpers.map_false "--no-build" build
  let args = Helpers.with_args args

  let command =
    ""
    |> Helpers.append_part "pack"
    |> Helpers.append_part no_restore
    |> Helpers.append_part no_build
    |> Helpers.append_part $"--configuration {configuration}"
    |> Helpers.append_part $"/p:Version={version}"
    |> Helpers.append_part "/p:TargetsForTfmSpecificContentInPackage="
    |> Helpers.append_part args

  [command_op command]

/// <summary>Publishes binaries via `dotnet publish`, optionally self-contained/trimmed and batched via a generated solution.</summary>
/// <param name="configuration" default="Debug" example="&quot;Release&quot;">Configuration (defaults to `Debug`).</param>
/// <param name="restore" default="true" example="&quot;true&quot;">Perform restore (omit to add `--no-restore`).</param>
/// <param name="build" default="true" example="&quot;true&quot;">Build before publish (omit to add `--no-build`).</param>
/// <param name="runtime" default="" example="&quot;linux-x64&quot;">Runtime identifier (`-r`).</param>
/// <param name="trim" default="false" example="true">Adds `-p:PublishTrimmed=true`.</param>
/// <param name="single" default="false" example="true">Publishes self-contained (`--self-contained`).</param>
/// <param name="args" default="" example="&quot;--version-suffix beta&quot;">Additional arguments for `dotnet publish`.</param>
[<export>] let publish (context: Protocol.ActionContext) (configuration: string option) (restore: bool option) (build: bool option) (runtime: string option) (trim: bool option) (single: bool option) (args: string option) =
  let configuration = Helpers.with_configuration configuration
  let no_restore = Helpers.map_false "--no-restore" restore
  let no_build = Helpers.map_false "--no-build" build
  let runtime = Helpers.map_value (fun value -> $"-r {value}") runtime
  let trim = Helpers.map_true "-p:PublishTrimmed=true" trim
  let single = Helpers.map_true "--self-contained" single
  let args = Helpers.with_args args

  let create_command project_file =
    ""
    |> Helpers.append_part "publish"
    |> Helpers.append_part project_file
    |> Helpers.append_part no_restore
    |> Helpers.append_part no_build
    |> Helpers.append_part $"--configuration {configuration}"
    |> Helpers.append_part runtime
    |> Helpers.append_part trim
    |> Helpers.append_part single
    |> Helpers.append_part args
    |> command_op

  with_batch_projects context create_command

/// <summary>Runs tests via `dotnet test`, optionally batched through a generated solution.</summary>
/// <param name="configuration" default="Debug" example="&quot;Release&quot;">Configuration (defaults to `Debug`).</param>
/// <param name="restore" default="true" example="&quot;true&quot;">Perform restore (omit to add `--no-restore`).</param>
/// <param name="build" default="true" example="&quot;true&quot;">Build before testing (omit to add `--no-build`).</param>
/// <param name="filter" default="" example="&quot;TestCategory!=integration&quot;">Test filter expression (`--filter`).</param>
/// <param name="args" default="" example="&quot;--blame-hang&quot;">Additional arguments for `dotnet test`.</param>
[<export>] let test (context: Protocol.ActionContext) (configuration: string option) (restore: bool option) (build: bool option) (filter: string option) (args: string option) =
  let configuration = with_configuration configuration
  let no_restore = Helpers.map_false "--no-restore" restore
  let no_build = Helpers.map_false "--no-build" build
  let filter = Helpers.map_value (fun value -> $"--filter \"{value}\"") filter
  let args = Helpers.with_args args

  let create_command project_file =
    ""
    |> Helpers.append_part "test"
    |> Helpers.append_part project_file
    |> Helpers.append_part no_restore
    |> Helpers.append_part no_build
    |> Helpers.append_part $"--configuration {configuration}"
    |> Helpers.append_part filter
    |> Helpers.append_part args
    |> command_op

  with_batch_projects context create_command

// Exported functions and Terrabuild execution flags.
{
  [nameof defaults] = [Default]
  [nameof dispatch] = [Dispatch; Never]
  [nameof tool] = [Local]
  [nameof restore] = [Local; Batchable]
  [nameof build] = [Remote; Batchable]
  [nameof pack] = [Remote]
  [nameof publish] = [Remote; Batchable]
  [nameof test] = [Remote; Batchable]
}
