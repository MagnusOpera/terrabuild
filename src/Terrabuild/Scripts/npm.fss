import "_protocol.fss" as Protocol
import "_helpers.fss" as Helpers

/// <summary>Provides build, test, and exec helpers for projects that use **npm** and a `package.json` at the project root. Infers dependencies from `package.json` to wire Terrabuild graphs and defaults outputs to `dist/**`.</summary>
type Package = { name: string; dependencies: string map option; devDependencies: string map option }

let load_package directory =
  let package_file = Fs.combinePath directory "package.json"
  let content = Fs.readText package_file |> Option.defaultValue ""
  Json.deserialize (typeof Package) content

let collect_local_dependency acc key value =
  match Regex.matchGroups "^file:(.*)$" value with
  | Some [project] -> project :: acc
  | _ -> acc

let collect_local_dependencies map =
  Map.fold collect_local_dependency [] map

let dependencies_from (package: Package) =
  let dependencies =
    package.dependencies
    |> Option.map collect_local_dependencies
    |> Option.defaultValue []

  let dev_dependencies =
    package.devDependencies
    |> Option.map collect_local_dependencies
    |> Option.defaultValue []

  List.append dependencies dev_dependencies
  |> List.distinct

/// <summary>Infers project metadata from `package.json` (dependencies and default outputs).</summary>
/// <param name="ignores" example="[ &quot;node_modules/**&quot; ]"></param>
/// <param name="outputs" example="[ &quot;dist/**&quot; ]"></param>
[<export>] let defaults (context: Protocol.ActionContext) =
  let dependencies =
    context.Directory
    |> load_package
    |> Option.map dependencies_from
    |> Option.defaultValue []

  { Id = None; Outputs = ["dist/**"]; Dependencies = dependencies }

/// <summary>Runs an arbitrary npm command (forwards the Terrabuild action name to `npm`).</summary>
/// <param name="args" default="" example="&quot;--port=1337&quot;">Additional arguments appended to the npm command.</param>
[<export>] let dispatch (context: Protocol.ActionContext) (args: string option) =
  let args = args |> Helpers.with_args
  let command =
    ""
    |> Helpers.append_part context.Command
    |> Helpers.append_part args

  [{ Command = "npm"; Arguments = command; ErrorLevel = 0 }]

/// <summary>Installs packages with `npm ci`, honoring the lock file.</summary>
/// <param name="force" default="false" example="true">Force install to bypass failed checks (see`--force`).</param>
/// <param name="clean" default="true" example="true">Enable clean install (see `clean-install`).</param>
/// <param name="args" default="" example="&quot;--install-strategy hoisted&quot;">Additional arguments passed to `npm ci`.</param>
[<export>] let install (context: Protocol.ActionContext) (force: bool option) (clean: bool option) (args: string option) =
  let force = force |> Helpers.map_true "--force"
  let clean = clean |> Helpers.map_true_default true "clean-"
  let args = args |> Helpers.with_args

  let command =
    ""
    |> Helpers.append_part $"{clean}install"
    |> Helpers.append_part force
    |> Helpers.append_part args

  [{ Command = "npm"; Arguments = command; ErrorLevel = 0 }]

/// <summary>Runs the `build` script via `npm run build`.</summary>
/// <param name="args" default="" example="&quot;--workspaces&quot;">Extra arguments forwarded after `--`.</param>
[<export>] let build (context: Protocol.ActionContext) (args: string option) =
  let args = args |> Helpers.with_args
  let command =
    ""
    |> Helpers.append_part "run build --"
    |> Helpers.append_part args

  [{ Command = "npm"; Arguments = command; ErrorLevel = 0 }]

/// <summary>Runs the `test` script via `npm run test`.</summary>
/// <param name="args" default="" example="&quot;--port=1337&quot;">Extra arguments forwarded after `--`.</param>
[<export>] let test (context: Protocol.ActionContext) (args: string option) =
  let args = args |> Helpers.with_args
  let command =
    ""
    |> Helpers.append_part "run test --"
    |> Helpers.append_part args

  [{ Command = "npm"; Arguments = command; ErrorLevel = 0 }]

/// <summary>Runs an arbitrary npm script via `npm run &lt;target&gt;`.</summary>
/// <param name="target" example="&quot;build-prod&quot;">Script target to invoke.</param>
/// <param name="args" default="" example="&quot;build-prod&quot;">Extra arguments forwarded after `--`.</param>
[<export>] let run (context: Protocol.ActionContext) (target: string) (args: string option) =
  let args = args |> Helpers.with_args
  let command =
    ""
    |> Helpers.append_part $"run {target} --"
    |> Helpers.append_part args

  [{ Command = "npm"; Arguments = command; ErrorLevel = 0 }]

/// <summary>Executes a package binary via `npm exec`.</summary>
/// <param name="package" example="&quot;hello-world-npm&quot;">Package to run with `npm exec`.</param>
/// <param name="args" default="" example="&quot;build-prod&quot;">Arguments passed to the package command.</param>
[<export>] let exec (context: Protocol.ActionContext) (package: string) (args: string option) =
  let args = args |> Helpers.with_args
  let command =
    ""
    |> Helpers.append_part "exec --"
    |> Helpers.append_part package
    |> Helpers.append_part args

  [{ Command = "npm"; Arguments = command; ErrorLevel = 0 }]

// Exported functions and Terrabuild execution flags.
{
  [nameof defaults] = [Default]
  [nameof dispatch] = [Dispatch; Never]
  [nameof install] = [Local]
  [nameof build] = [Remote]
  [nameof test] = [Remote]
  [nameof run] = [Local]
  [nameof exec] = [Local]
}
