/// <summary>Provides build, test, and exec helpers for projects that use **npm** and a `package.json` at the project root. Infers dependencies from `package.json` to wire Terrabuild graphs and defaults outputs to `dist/**`.</summary>
type Package = { name: string; dependencies: string map option; devDependencies: string map option }

let append_part part acc =
  match (part, acc) with
  | ("", _) -> acc
  | (_, "") -> part
  | _ -> $"{acc} {part}"

let map_true flag value =
  match value with
  | Some true -> flag
  | _ -> ""

let with_args args =
  args |> Option.defaultValue ""

let load_package directory =
  let package_file = Fs.combinePath directory "package.json"
  let content = Fs.readText package_file |> Option.defaultValue ""
  Json.deserialize (typeof Package) content

let collect_local_dependency acc key value =
  match Regex.matchGroups "^file:(.*)$" value with
  | Some [project] -> project :: acc
  | _ -> acc

let collect_local_dependencies map =
  Map.fold collect_local_dependency [] map

let dependencies_from (package: Package) =
  let dependencies =
    package.dependencies
    |> Option.map collect_local_dependencies
    |> Option.defaultValue []

  let dev_dependencies =
    package.devDependencies
    |> Option.map collect_local_dependencies
    |> Option.defaultValue []

  List.append dependencies dev_dependencies
  |> List.distinct

/// <summary>Infers project metadata from `package.json` (dependencies and default outputs).</summary>
/// <param name="ignores" required="false" example="[ &quot;node_modules/**&quot; ]"></param>
/// <param name="outputs" required="false" example="[ &quot;dist/**&quot; ]"></param>
export let defaults (context: { Directory: string }) =
  let dependencies =
    context.Directory
    |> load_package
    |> Option.map dependencies_from
    |> Option.defaultValue []

  { Id = None; Outputs = ["dist/**"]; Dependencies = dependencies }

/// <summary>Runs an arbitrary npm command (forwards the Terrabuild action name to `npm`).</summary>
/// <param name="args" required="false" example="&quot;--port=1337&quot;">Additional arguments appended to the npm command.</param>
export let dispatch (context: { Command: string }) (args: string option) =
  let command =
    ""
    |> append_part context.Command
    |> append_part (with_args args)

  [{ Command = "npm"; Arguments = command; ErrorLevel = 0 }]

/// <summary>Installs packages with `npm ci`, honoring the lock file.</summary>
/// <param name="force" required="false" example="true">Adds `--force` to bypass failed checks.</param>
/// <param name="clean" required="false" example="true">Enable clean install; set `true` to enforce `clean-install`.</param>
/// <param name="args" required="false" example="&quot;--install-strategy hoisted&quot;">Additional arguments passed to `npm ci`.</param>
export let install (context: { Command: string }) (force: bool option) (clean: bool option) (args: string option) =
  let force = force |> map_true "--force"
  let clean = clean |> map_true "clean-"
  let args = with_args args

  let command =
    ""
    |> append_part $"{clean}install"
    |> append_part force
    |> append_part args

  [{ Command = "npm"; Arguments = command; ErrorLevel = 0 }]

/// <summary>Runs the `build` script via `npm run build`.</summary>
/// <param name="args" required="false" example="&quot;--workspaces&quot;">Extra arguments forwarded after `--`.</param>
export let build (context: { Command: string }) (args: string option) =
  let command =
    ""
    |> append_part "run build --"
    |> append_part (with_args args)

  [{ Command = "npm"; Arguments = command; ErrorLevel = 0 }]

/// <summary>Runs the `test` script via `npm run test`.</summary>
/// <param name="args" required="false" example="&quot;--port=1337&quot;">Extra arguments forwarded after `--`.</param>
export let test (context: { Command: string }) (args: string option) =
  let command =
    ""
    |> append_part "run test --"
    |> append_part (with_args args)

  [{ Command = "npm"; Arguments = command; ErrorLevel = 0 }]

/// <summary>Runs an arbitrary npm script via `npm run &lt;target&gt;`.</summary>
/// <param name="target" required="false" example="&quot;build-prod&quot;">Script target to invoke.</param>
/// <param name="args" required="false" example="&quot;build-prod&quot;">Extra arguments forwarded after `--`.</param>
export let run (context: { Command: string }) (target: string) (args: string option) =
  let command =
    ""
    |> append_part $"run {target} --"
    |> append_part (with_args args)

  [{ Command = "npm"; Arguments = command; ErrorLevel = 0 }]

/// <summary>Executes a package binary via `npm exec`.</summary>
/// <param name="package" required="false" example="&quot;hello-world-npm&quot;">Package to run with `npm exec`.</param>
/// <param name="args" required="false" example="&quot;build-prod&quot;">Arguments passed to the package command.</param>
export let exec (context: { Command: string }) (package: string) (args: string option) =
  let command =
    ""
    |> append_part "exec --"
    |> append_part package
    |> append_part (with_args args)

  [{ Command = "npm"; Arguments = command; ErrorLevel = 0 }]


type ExportFlag =
  | Dispatch
  | Default
  | Batchable
  | Never
  | Local
  | External
  | Remote

{
  [nameof defaults] = [Default]
  [nameof dispatch] = [Dispatch; Never]
  [nameof install] = [Local]
  [nameof build] = [Remote]
  [nameof test] = [Remote]
  [nameof run] = [Local]
  [nameof exec] = [Local]
}
