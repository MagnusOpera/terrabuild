import "_protocol.fss" as Protocol
import "_helpers.fss" as Helpers

/// <summary>pnpm extension for install, build, and test workflows.</summary>
type Package =
    { name: string
      dependencies: string map option
      devDependencies: string map option }

let load_package directory =
  let package_file = Fs.combinePath directory "package.json"
  let content = Fs.readText package_file |> Option.defaultValue ""
  Json.deserialize (typeof Package) content

let collect_workspace_dependency acc key value =
  match value with
  | "workspace:*" -> key :: acc
  | _ -> acc

let collect_workspace_dependencies map =
  Map.fold collect_workspace_dependency [] map

let dependencies_from (package: Package) =
  let dependencies =
    package.dependencies
    |> Option.map collect_workspace_dependencies
    |> Option.defaultValue []

  let dev_dependencies =
    package.devDependencies
    |> Option.map collect_workspace_dependencies
    |> Option.defaultValue []

  List.append dependencies dev_dependencies
  |> List.distinct

let batch_filters (context: Protocol.ActionContext) =
  match context.Batch with
  | Some batch ->
      batch.ProjectPaths
      |> List.fold (fun acc project -> $"{acc} --filter ./{project}") "--recursive"
  | _ -> ""

/// <summary>Computes default outputs and dependencies from `package.json`.</summary>
/// <param name="ignores" example="[ &quot;node_modules/**&quot; ]"></param>
/// <param name="outputs" example="[ &quot;dist/**&quot; ]"></param>
[<export>] let defaults (context: Protocol.ActionContext) =
  let package = load_package context.Directory

  let id =
    package
    |> Option.map (fun value -> value.name)

  let dependencies =
    package
    |> Option.map dependencies_from
    |> Option.defaultValue []

  { Id = id; Outputs = ["dist/**"]; Dependencies = dependencies }

/// <summary>Runs an arbitrary pnpm command (Terrabuild action name is forwarded to `pnpm`).</summary>
/// <param name="args" default="" example="&quot;store status&quot;">Extra arguments for the pnpm command.</param>
[<export>] let dispatch (context: Protocol.ActionContext) (args: string option) =
  let args = args |> Helpers.with_args
  let command =
    ""
    |> Helpers.append_part context.Command
    |> Helpers.append_part args

  { Batchable = false
    Operations = [ { Command = "pnpm"
                     Arguments = command
                     ErrorLevel = 0 } ] }

/// <summary>Installs packages with `pnpm install`, optionally honoring the lockfile and batching across workspaces.</summary>
/// <param name="force" default="true" example="true">Adds `--force`.</param>
/// <param name="frozen" default="true" example="true">Adds `--frozen-lockfile`.</param>
/// <param name="args" default="" example="&quot;--no-color&quot;">Extra `pnpm install` arguments.</param>
[<export>] let install (context: Protocol.ActionContext) (force: bool option) (frozen: bool option) (args: string option) =
  let filters = batch_filters context
  let frozen = frozen |> Helpers.map_true_default true "--frozen-lockfile"
  let force = force |> Helpers.map_true_default true "--force"
  let args = args |> Helpers.with_args

  let command =
    ""
    |> Helpers.append_part filters
    |> Helpers.append_part "install"
    |> Helpers.append_part frozen
    |> Helpers.append_part "--link-workspace-packages"
    |> Helpers.append_part force
    |> Helpers.append_part args

  { Batchable = true
    Operations = [ { Command = "pnpm"
                     Arguments = command
                     ErrorLevel = 0 } ] }

/// <summary>Runs the `build` script (`pnpm run build`) across targeted workspaces.</summary>
/// <param name="args" default="" example="&quot;--no-color&quot;">Extra script arguments.</param>
[<export>] let build (context: Protocol.ActionContext) (args: string option) =
  let filters = batch_filters context
  let args = args |> Helpers.with_args

  let command =
    ""
    |> Helpers.append_part filters
    |> Helpers.append_part "run build"
    |> Helpers.append_part args

  { Batchable = true
    Operations = [ { Command = "pnpm"
                     Arguments = command
                     ErrorLevel = 0 } ] }

/// <summary>Runs the `test` script (`pnpm run test`) across targeted workspaces.</summary>
/// <param name="args" default="" example="&quot;--runInBand&quot;">Extra script arguments.</param>
[<export>] let test (context: Protocol.ActionContext) (args: string option) =
  let filters = batch_filters context
  let args = args |> Helpers.with_args

  let command =
    ""
    |> Helpers.append_part filters
    |> Helpers.append_part "run test"
    |> Helpers.append_part args

  { Batchable = true
    Operations = [ { Command = "pnpm"
                     Arguments = command
                     ErrorLevel = 0 } ] }

/// <summary>Runs an arbitrary pnpm script (`pnpm run &lt;target&gt;`).</summary>
/// <param name="target" example="&quot;build:prod&quot;">Script name.</param>
/// <param name="args" default="" example="&quot;--watch&quot;">Extra script arguments.</param>
/// <param name="recursive" default="true" example="true">Adds `--recursive`.</param>
[<export>] let run (context: Protocol.ActionContext) (target: string) (recursive: bool option) (args: string option) =
  let recursive = recursive |> Helpers.map_true_default true "--recursive"
  let args = args |> Helpers.with_args

  let command =
    ""
    |> Helpers.append_part recursive
    |> Helpers.append_part $"run {target}"  
    |> Helpers.append_part args

  { Batchable = false
    Operations = [ { Command = "pnpm"
                     Arguments = command
                     ErrorLevel = 0 } ] }

// Exported functions and Terrabuild execution flags.
{ [nameof defaults] = [Default]
  [nameof dispatch] = [Dispatch; Never]
  [nameof install] = [Local]
  [nameof build] = [Remote]
  [nameof test] = [Remote]
  [nameof run] = [Local] }
