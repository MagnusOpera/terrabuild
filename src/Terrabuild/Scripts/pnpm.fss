import "_protocol.fss"
import "_helpers.fss"

/// <summary>Provides build/install/test helpers for projects using **pnpm** and a `package.json`. Supports batch (cascade) builds when `pnpm-workspace.yaml` and a root `package.json` exist at the Terrabuild workspace root.</summary>
type Package = { name: string; dependencies: string map option; devDependencies: string map option }

let load_package directory =
  let package_file = Fs.combinePath directory "package.json"
  let content = Fs.readText package_file |> Option.defaultValue ""
  Json.deserialize (typeof Package) content

let collect_workspace_dependency acc key value =
  match value with
  | "workspace:*" -> key :: acc
  | _ -> acc

let collect_workspace_dependencies map =
  Map.fold collect_workspace_dependency [] map

let dependencies_from (package: Package) =
  let dependencies =
    package.dependencies
    |> Option.map collect_workspace_dependencies
    |> Option.defaultValue []

  let dev_dependencies =
    package.devDependencies
    |> Option.map collect_workspace_dependencies
    |> Option.defaultValue []

  List.append dependencies dev_dependencies
  |> List.distinct

let batch_filters (context: ActionContext) =
  match context.Batch with
  | Some batch ->
      batch.ProjectPaths
      |> List.fold (fun acc project -> $"{acc} --filter ./{project}") "--recursive"
  | _ -> ""

/// <summary>Infers project metadata from `package.json` (dependencies and default outputs).</summary>
/// <param name="ignores" required="false" example="[ &quot;node_modules/**&quot; ]"></param>
/// <param name="outputs" required="false" example="[ &quot;dist/**&quot; ]"></param>
[<export>] let defaults (context: ActionContext) =
  let package = load_package context.Directory

  let id =
    package
    |> Option.map (fun value -> value.name)

  let dependencies =
    package
    |> Option.map dependencies_from
    |> Option.defaultValue []

  { Id = id; Outputs = ["dist/**"]; Dependencies = dependencies }

/// <summary>Runs an arbitrary pnpm command (Terrabuild action name is forwarded to `pnpm`).</summary>
/// <param name="args" required="false" example="&quot;--port=1337&quot;">Additional arguments appended after the pnpm command.</param>
[<export>] let dispatch (context: ActionContext) (args: string option) =
  let command =
    ""
    |> _helpers.append_part context.Command
    |> _helpers.append_part (_helpers.with_args args)

  [{ Command = "pnpm"; Arguments = command; ErrorLevel = 0 }]

/// <summary>Installs packages with `pnpm install`, optionally honoring the lockfile and batching across workspaces.</summary>
/// <param name="force" required="false" example="true">Adds `--force` to reinstall when checks fail.</param>
/// <param name="frozen" required="false" example="true">Enable frozen versions; set `true` to enforce `--frozen-lockfile`.</param>
/// <param name="args" required="false" example="&quot;--no-color&quot;">Additional arguments for `pnpm install`.</param>
[<export>] let install (context: ActionContext) (force: bool option) (frozen: bool option) (args: string option) =
  let filters = batch_filters context
  let frozen = frozen |> _helpers.map_true "--frozen-lockfile"
  let force = force |> _helpers.map_true "--force"
  let args = _helpers.with_args args

  let command =
    ""
    |> _helpers.append_part filters
    |> _helpers.append_part "install"
    |> _helpers.append_part frozen
    |> _helpers.append_part "--link-workspace-packages"
    |> _helpers.append_part force
    |> _helpers.append_part args

  [{ Command = "pnpm"; Arguments = command; ErrorLevel = 0 }]

/// <summary>Runs the `build` script (`pnpm run build`) across targeted workspaces.</summary>
/// <param name="args" required="false" example="&quot;--no-color&quot;">Additional arguments for the script.</param>
[<export>] let build (context: ActionContext) (args: string option) =
  let filters = batch_filters context
  let args = _helpers.with_args args

  let command =
    ""
    |> _helpers.append_part filters
    |> _helpers.append_part "run build"
    |> _helpers.append_part args

  [{ Command = "pnpm"; Arguments = command; ErrorLevel = 0 }]

/// <summary>Runs the `test` script (`pnpm run test`) across targeted workspaces.</summary>
/// <param name="args" required="false" example="&quot;--port=1337&quot;">Additional arguments for the script.</param>
[<export>] let test (context: ActionContext) (args: string option) =
  let filters = batch_filters context
  let args = _helpers.with_args args

  let command =
    ""
    |> _helpers.append_part filters
    |> _helpers.append_part "run test"
    |> _helpers.append_part args

  [{ Command = "pnpm"; Arguments = command; ErrorLevel = 0 }]

/// <summary>Runs an arbitrary pnpm script (`pnpm run &lt;target&gt;`).</summary>
/// <param name="target" required="false" example="&quot;build-prod&quot;">Target to invoke.</param>
/// <param name="args" required="false" example="&quot;build-prod&quot;">Additional arguments forwarded to the script.</param>
/// <param name="no_recursive" required="false" example="true">Skip `--recursive` when targeting a single workspace.</param>
[<export>] let run (context: ActionContext) (target: string) (no_recursive: bool option) (args: string option) =
  let recursive = no_recursive |> _helpers.map_false "--recursive"
  let args = _helpers.with_args args

  let command =
    ""
    |> _helpers.append_part recursive
    |> _helpers.append_part $"run {target}"
    |> _helpers.append_part args

  [{ Command = "pnpm"; Arguments = command; ErrorLevel = 0 }]

// Exported functions and Terrabuild execution flags.
{
  [nameof defaults] = [Default]
  [nameof dispatch] = [Dispatch; Never]
  [nameof install] = [Local; Batchable]
  [nameof build] = [Remote; Batchable]
  [nameof test] = [Remote; Batchable]
  [nameof run] = [Local]
}
