import "_protocol.fss" as Protocol
import "_helpers.fss" as Helpers

/// <summary>Provides wrappers around the Terraform CLI (init/plan/apply/destroy) for projects that manage their own state.</summary>
let format_vars variables =
  variables
  |> Option.map (Map.fold (fun acc key value -> acc |> Helpers.append_part $"-var=\"{key}={value}\"") "")
  |> Option.defaultValue ""

/// <summary>Declares default outputs for Terraform runs.</summary>
/// <param name="ignores" required="false" example="[ &quot;.terraform/&quot; &quot;*.tfstate/&quot; ]"></param>
/// <param name="outputs" required="false" example="[ &quot;*.planfile&quot; ]"></param>
[<export>] let defaults (context: Protocol.ActionContext) =
  { Id = None; Outputs = ["*.planfile"]; Dependencies = [] }

/// <summary>Runs an arbitrary Terraform command (action name is forwarded to `terraform`).</summary>
/// <param name="args" required="false" example="&quot;fmt -write=false&quot;">Arguments appended after the Terraform subcommand.</param>
[<export>] let dispatch (context: Protocol.ActionContext) (args: string option) =
  let command =
    ""
    |> Helpers.append_part context.Command
    |> Helpers.append_part (Helpers.with_args args)
  [{ Command = "terraform"; Arguments = command; ErrorLevel = 0 }]

/// <summary>Initializes Terraform providers and backend.</summary>
/// <param name="config" required="false" example="&quot;backend.prod.config&quot;">Backend config file passed to `terraform init -backend-config`.</param>
/// <param name="args" required="false" example="&quot;-upgrade&quot;">Additional arguments for `terraform init`.</param>
[<export>] let init (context: Protocol.ActionContext) (config: string option) (args: string option) =
  let config = config |> Helpers.map_value (fun value -> $"-backend-config={value}")
  let args = Helpers.with_args args
  let command =
    ""
    |> Helpers.append_part "init"
    |> Helpers.append_part "-reconfigure"
    |> Helpers.append_part config
    |> Helpers.append_part args
  [{ Command = "terraform"; Arguments = command; ErrorLevel = 0 }]

/// <summary>Validates configuration before planning/applying.</summary>
/// <title>Generate plan file.</title>
/// <param name="args" required="false" example="&quot;-no-color&quot;">Additional arguments for `terraform validate`.</param>
[<export>] let validate (context: Protocol.ActionContext) (args: string option) =
  let command =
    ""
    |> Helpers.append_part "validate"
    |> Helpers.append_part (Helpers.with_args args)
  [{ Command = "terraform"; Arguments = command; ErrorLevel = 0 }]

/// <summary>Selects or creates a Terraform workspace before planning/applying.</summary>
/// <title>Select workspace.</title>
/// <param name="workspace" required="false" example="&quot;dev&quot;">Workspace to use. Use `default` if not provided.</param>
/// <param name="create" required="false" example="true">Create the workspace when it does not exist.</param>
/// <param name="args" required="false" example="&quot;-no-color&quot;">Additional arguments for `terraform workspace select`.</param>
[<export>] let select (context: Protocol.ActionContext) (workspace: string option) (create: bool option) (args: string option) =
  match workspace with
  | Some workspace ->
      let command =
        ""
        |> Helpers.append_part "workspace select"
        |> Helpers.append_part (Helpers.map_true "-or-create" create)
        |> Helpers.append_part workspace
        |> Helpers.append_part (Helpers.with_args args)
      [{ Command = "terraform"; Arguments = command; ErrorLevel = 0 }]
  | _ -> []

/// <summary>Generates a plan file for review/apply.</summary>
/// <title>Generate plan file.</title>
/// <param name="variables" required="false" example="{ configuration: &quot;Release&quot; }">Variables passed as `-var` assignments.</param>
/// <param name="args" required="false" example="&quot;-no-color&quot;">Additional arguments for `terraform plan`.</param>
[<export>] let plan (context: Protocol.ActionContext) (variables: string map option) (args: string option) =
  let vars = format_vars variables
  let args = Helpers.with_args args
  let command =
    ""
    |> Helpers.append_part "plan"
    |> Helpers.append_part "-out=terrabuild.planfile"
    |> Helpers.append_part vars
    |> Helpers.append_part args
  [{ Command = "terraform"; Arguments = command; ErrorLevel = 0 }]

/// <summary>Applies the generated plan (or runs `apply` without a plan when requested).</summary>
/// <title>Apply plan file.</title>
/// <param name="no_plan" required="false" example="true">Run `apply` without `-out` plan file.</param>
/// <param name="args" required="false" example="&quot;-no-color&quot;">Additional arguments for `terraform apply`.</param>
[<export>] let apply (context: Protocol.ActionContext) (no_plan: bool option) (args: string option) =
  let planfile = Helpers.map_false "terrabuild.planfile" no_plan
  let args = Helpers.with_args args
  let command =
    ""
    |> Helpers.append_part "apply"
    |> Helpers.append_part "-input=false"
    |> Helpers.append_part planfile
    |> Helpers.append_part args
  [{ Command = "terraform"; Arguments = command; ErrorLevel = 0 }]

/// <summary>Destroys all managed resources for the current workspace.</summary>
/// <title>Destroy the deployment.</title>
/// <param name="variables" required="false" example="{ configuration: &quot;Release&quot; }">Variables passed as `-var` assignments.</param>
/// <param name="args" required="false" example="&quot;&quot;">Additional arguments for `terraform destroy`.</param>
[<export>] let destroy (context: Protocol.ActionContext) (variables: string map option) (args: string option) =
  let vars = format_vars variables
  let args = Helpers.with_args args
  let command =
    ""
    |> Helpers.append_part "destroy"
    |> Helpers.append_part "-input=false"
    |> Helpers.append_part vars
    |> Helpers.append_part args
  [{ Command = "terraform"; Arguments = command; ErrorLevel = 0 }]

// Exported functions and Terrabuild execution flags.
{
  [nameof defaults] = [Default]
  [nameof dispatch] = [Dispatch; Never]
  [nameof init] = [Local]
  [nameof validate] = [Remote]
  [nameof select] = [Never]
  [nameof plan] = [Remote]
  [nameof apply] = [Remote]
  [nameof destroy] = [Remote]
}
