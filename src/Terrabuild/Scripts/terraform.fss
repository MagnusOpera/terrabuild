/// <summary>Provides wrappers around the Terraform CLI (init/plan/apply/destroy) for projects that manage their own state.</summary>
let append_part part acc =
  match (part, acc) with
  | ("", _) -> acc
  | (_, "") -> part
  | _ -> $"{acc} {part}"

let map_true flag value =
  match value with
  | Some true -> flag
  | _ -> ""

let map_false flag value =
  match value with
  | Some true -> ""
  | _ -> flag

let map_value mapper value =
  value |> Option.map mapper |> Option.defaultValue ""

let with_args args =
  args |> Option.defaultValue ""

let format_vars variables =
  variables
  |> Option.map (Map.fold (fun acc key value -> acc |> append_part $"-var=\"{key}={value}\"") "")
  |> Option.defaultValue ""

/// <summary>Declares default outputs for Terraform runs.</summary>
/// <param name="ignores" required="false" example="[ &quot;.terraform/&quot; &quot;*.tfstate/&quot; ]"></param>
/// <param name="outputs" required="false" example="[ &quot;*.planfile&quot; ]"></param>
[<export>] let defaults (context: { Directory: string }) =
  { Id = None; Outputs = ["*.planfile"]; Dependencies = [] }

/// <summary>Runs an arbitrary Terraform command (action name is forwarded to `terraform`).</summary>
/// <param name="args" required="false" example="&quot;fmt -write=false&quot;">Arguments appended after the Terraform subcommand.</param>
[<export>] let dispatch (context: { Command: string }) (args: string option) =
  let command =
    ""
    |> append_part context.Command
    |> append_part (with_args args)
  [{ Command = "terraform"; Arguments = command; ErrorLevel = 0 }]

/// <summary>Initializes Terraform providers and backend.</summary>
/// <param name="config" required="false" example="&quot;backend.prod.config&quot;">Backend config file passed to `terraform init -backend-config`.</param>
/// <param name="args" required="false" example="&quot;-upgrade&quot;">Additional arguments for `terraform init`.</param>
[<export>] let init (context: { Command: string }) (config: string option) (args: string option) =
  let config = config |> map_value (fun value -> $"-backend-config={value}")
  let args = with_args args
  let command =
    ""
    |> append_part "init"
    |> append_part "-reconfigure"
    |> append_part config
    |> append_part args
  [{ Command = "terraform"; Arguments = command; ErrorLevel = 0 }]

/// <summary>Validates configuration before planning/applying.</summary>
/// <title>Generate plan file.</title>
/// <param name="args" required="false" example="&quot;-no-color&quot;">Additional arguments for `terraform validate`.</param>
[<export>] let validate (context: { Command: string }) (args: string option) =
  let command =
    ""
    |> append_part "validate"
    |> append_part (with_args args)
  [{ Command = "terraform"; Arguments = command; ErrorLevel = 0 }]

/// <summary>Selects or creates a Terraform workspace before planning/applying.</summary>
/// <title>Select workspace.</title>
/// <param name="workspace" required="false" example="&quot;dev&quot;">Workspace to use. Use `default` if not provided.</param>
/// <param name="create" required="false" example="true">Create the workspace when it does not exist.</param>
/// <param name="args" required="false" example="&quot;-no-color&quot;">Additional arguments for `terraform workspace select`.</param>
[<export>] let select (context: { Command: string }) (workspace: string option) (create: bool option) (args: string option) =
  match workspace with
  | Some workspace ->
      let command =
        ""
        |> append_part "workspace select"
        |> append_part (map_true "-or-create" create)
        |> append_part workspace
        |> append_part (with_args args)
      [{ Command = "terraform"; Arguments = command; ErrorLevel = 0 }]
  | _ -> []

/// <summary>Generates a plan file for review/apply.</summary>
/// <title>Generate plan file.</title>
/// <param name="variables" required="false" example="{ configuration: &quot;Release&quot; }">Variables passed as `-var` assignments.</param>
/// <param name="args" required="false" example="&quot;-no-color&quot;">Additional arguments for `terraform plan`.</param>
[<export>] let plan (context: { Command: string }) (variables: string map option) (args: string option) =
  let vars = format_vars variables
  let args = with_args args
  let command =
    ""
    |> append_part "plan"
    |> append_part "-out=terrabuild.planfile"
    |> append_part vars
    |> append_part args
  [{ Command = "terraform"; Arguments = command; ErrorLevel = 0 }]

/// <summary>Applies the generated plan (or runs `apply` without a plan when requested).</summary>
/// <title>Apply plan file.</title>
/// <param name="no_plan" required="false" example="true">Run `apply` without `-out` plan file.</param>
/// <param name="args" required="false" example="&quot;-no-color&quot;">Additional arguments for `terraform apply`.</param>
[<export>] let apply (context: { Command: string }) (no_plan: bool option) (args: string option) =
  let planfile = map_false "terrabuild.planfile" no_plan
  let args = with_args args
  let command =
    ""
    |> append_part "apply"
    |> append_part "-input=false"
    |> append_part planfile
    |> append_part args
  [{ Command = "terraform"; Arguments = command; ErrorLevel = 0 }]

/// <summary>Destroys all managed resources for the current workspace.</summary>
/// <title>Destroy the deployment.</title>
/// <param name="variables" required="false" example="{ configuration: &quot;Release&quot; }">Variables passed as `-var` assignments.</param>
/// <param name="args" required="false" example="&quot;&quot;">Additional arguments for `terraform destroy`.</param>
[<export>] let destroy (context: { Command: string }) (variables: string map option) (args: string option) =
  let vars = format_vars variables
  let args = with_args args
  let command =
    ""
    |> append_part "destroy"
    |> append_part "-input=false"
    |> append_part vars
    |> append_part args
  [{ Command = "terraform"; Arguments = command; ErrorLevel = 0 }]


type ExportFlag =
  | Dispatch
  | Default
  | Batchable
  | Never
  | Local
  | External
  | Remote

{
  [nameof defaults] = [Default]
  [nameof dispatch] = [Dispatch; Never]
  [nameof init] = [Local]
  [nameof validate] = [Remote]
  [nameof select] = [Never]
  [nameof plan] = [Remote]
  [nameof apply] = [Remote]
  [nameof destroy] = [Remote]
}
