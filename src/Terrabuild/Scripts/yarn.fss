import "_protocol.fss" as Protocol
import "_helpers.fss" as Helpers

/// <summary>Provides build/test helpers for projects using **yarn** and a `package.json`. Defaults outputs to `dist/**` and infers dependencies from `package.json` to link Terrabuild graphs.</summary>
type Package = { name: string; dependencies: string map option; devDependencies: string map option }

let load_package directory =
  let package_file = Fs.combinePath directory "package.json"
  let content = Fs.readText package_file |> Option.defaultValue ""
  Json.deserialize (typeof Package) content

let collect_local_dependency acc key value =
  match Regex.matchGroups "^file:(.*)$" value with
  | Some [project] -> project :: acc
  | _ -> acc

let collect_local_dependencies map =
  Map.fold collect_local_dependency [] map

let dependencies_from (package: Package) =
  let dependencies =
    package.dependencies
    |> Option.map collect_local_dependencies
    |> Option.defaultValue []

  let dev_dependencies =
    package.devDependencies
    |> Option.map collect_local_dependencies
    |> Option.defaultValue []

  List.append dependencies dev_dependencies
  |> List.distinct

/// <summary>Infers project metadata from `package.json` (dependencies and default outputs).</summary>
/// <param name="ignores" example="[ &quot;node_modules/**&quot; ]"></param>
/// <param name="outputs" example="[ &quot;dist/**&quot; ]"></param>
[<export>] let defaults (context: Protocol.ActionContext) =
  let dependencies =
    context.Directory
    |> load_package
    |> Option.map dependencies_from
    |> Option.defaultValue []

  { Id = None; Outputs = ["dist/**"]; Dependencies = dependencies }

/// <summary>Runs an arbitrary yarn command (Terrabuild action name is forwarded to `yarn`).</summary>
/// <param name="args" default="" example="&quot;--port=1337&quot;">Additional arguments appended after the yarn command.</param>
[<export>] let dispatch (context: Protocol.ActionContext) (args: string option) =
  let command =
    ""
    |> Helpers.append_part context.Command
    |> Helpers.append_part "--"
    |> Helpers.append_part (Helpers.with_args args)

  [{ Command = "yarn"; Arguments = command; ErrorLevel = 0 }]

/// <summary>Installs packages with `yarn install`, optionally updating the lockfile or ignoring engines.</summary>
/// <param name="update" default="false" example="true">Allow lockfile updates (see `--frozen-lockfile`).</param>
/// <param name="ignore_engines" default="false" example="true">Ignore engines (see `--ignore-engines`).</param>
/// <param name="args" default="" example="&quot;--verbose&quot;">Additional arguments.</param>
[<export>] let install (context: Protocol.ActionContext) (update: bool option) (ignore_engines: bool option) (args: string option) =
  let update = update |> Helpers.map_false "--frozen-lockfile"
  let ignore_engines = ignore_engines |> Helpers.map_true "--ignore-engines"
  let args = Helpers.with_args args

  let command =
    ""
    |> Helpers.append_part "install"
    |> Helpers.append_part update
    |> Helpers.append_part ignore_engines
    |> Helpers.append_part args

  [{ Command = "yarn"; Arguments = command; ErrorLevel = 0 }]

/// <summary>Runs the `build` script via `yarn build`.</summary>
/// <param name="args" default="" example="&quot;--verbose&quot;">Additional arguments forwarded after `--`.</param>
[<export>] let build (context: Protocol.ActionContext) (args: string option) =
  let command =
    ""
    |> Helpers.append_part "build --"
    |> Helpers.append_part (Helpers.with_args args)

  [{ Command = "yarn"; Arguments = command; ErrorLevel = 0 }]

/// <summary>Runs the `test` script via `yarn test`.</summary>
/// <param name="args" default="" example="&quot;--verbose&quot;">Additional arguments forwarded after `--`.</param>
[<export>] let test (context: Protocol.ActionContext) (args: string option) =
  let command =
    ""
    |> Helpers.append_part "test --"
    |> Helpers.append_part (Helpers.with_args args)

  [{ Command = "yarn"; Arguments = command; ErrorLevel = 0 }]

/// <summary>Runs an arbitrary yarn script (`yarn &lt;command&gt;`).</summary>
/// <param name="command" example="&quot;build-prod&quot;">Command to run.</param>
/// <param name="args" default="" example="&quot;build-prod&quot;">Additional arguments forwarded after `--`.</param>
[<export>] let run (context: Protocol.ActionContext) (command: string) (args: string option) =
  let operation =
    ""
    |> Helpers.append_part command
    |> Helpers.append_part "--"
    |> Helpers.append_part (Helpers.with_args args)

  [{ Command = "yarn"; Arguments = operation; ErrorLevel = 0 }]

// Exported functions and Terrabuild execution flags.
{
  [nameof defaults] = [Default]
  [nameof dispatch] = [Dispatch; Never]
  [nameof install] = [Local]
  [nameof build] = [Remote]
  [nameof test] = [Remote]
  [nameof run] = [Local]
}
