import "_protocol.fss"
import "_helpers.fss"

/// <summary>Provides build/test helpers for projects using **yarn** and a `package.json`. Defaults outputs to `dist/**` and infers dependencies from `package.json` to link Terrabuild graphs.</summary>
type Package = { name: string; dependencies: string map option; devDependencies: string map option }

let load_package directory =
  let package_file = Fs.combinePath directory "package.json"
  let content = Fs.readText package_file |> Option.defaultValue ""
  Json.deserialize (typeof Package) content

let collect_local_dependency acc key value =
  match Regex.matchGroups "^file:(.*)$" value with
  | Some [project] -> project :: acc
  | _ -> acc

let collect_local_dependencies map =
  Map.fold collect_local_dependency [] map

let dependencies_from (package: Package) =
  let dependencies =
    package.dependencies
    |> Option.map collect_local_dependencies
    |> Option.defaultValue []

  let dev_dependencies =
    package.devDependencies
    |> Option.map collect_local_dependencies
    |> Option.defaultValue []

  List.append dependencies dev_dependencies
  |> List.distinct

/// <summary>Infers project metadata from `package.json` (dependencies and default outputs).</summary>
/// <param name="ignores" required="false" example="[ &quot;node_modules/**&quot; ]"></param>
/// <param name="outputs" required="false" example="[ &quot;dist/**&quot; ]"></param>
[<export>] let defaults (context: ActionContext) =
  let dependencies =
    context.Directory
    |> load_package
    |> Option.map dependencies_from
    |> Option.defaultValue []

  { Id = None; Outputs = ["dist/**"]; Dependencies = dependencies }

/// <summary>Runs an arbitrary yarn command (Terrabuild action name is forwarded to `yarn`).</summary>
/// <param name="args" required="false" example="&quot;--port=1337&quot;">Additional arguments appended after the yarn command.</param>
[<export>] let dispatch (context: ActionContext) (args: string option) =
  let command =
    ""
    |> _helpers.append_part context.Command
    |> _helpers.append_part "--"
    |> _helpers.append_part (_helpers.with_args args)

  [{ Command = "yarn"; Arguments = command; ErrorLevel = 0 }]

/// <summary>Installs packages with `yarn install`, optionally updating the lockfile or ignoring engines.</summary>
/// <param name="update" required="false" example="true">Allow lockfile updates (omit to enforce frozen lockfile).</param>
/// <param name="ignore-engines" required="false" example="true">Adds `--ignore-engines`.</param>
/// <param name="args" required="false" example="&quot;--verbose&quot;">Additional arguments for `yarn install`.</param>
[<export>] let install (context: ActionContext) (update: bool option) (ignoreEngines: bool option) (args: string option) =
  let update = update |> _helpers.map_false "--frozen-lockfile"
  let ignore_engines = ignoreEngines |> _helpers.map_true "--ignore-engines"
  let args = _helpers.with_args args

  let command =
    ""
    |> _helpers.append_part "install"
    |> _helpers.append_part update
    |> _helpers.append_part ignore_engines
    |> _helpers.append_part args

  [{ Command = "yarn"; Arguments = command; ErrorLevel = 0 }]

/// <summary>Runs the `build` script via `yarn build`.</summary>
/// <param name="args" required="false" example="&quot;--verbose&quot;">Additional arguments forwarded after `--`.</param>
[<export>] let build (context: ActionContext) (args: string option) =
  let command =
    ""
    |> _helpers.append_part "build --"
    |> _helpers.append_part (_helpers.with_args args)

  [{ Command = "yarn"; Arguments = command; ErrorLevel = 0 }]

/// <summary>Runs the `test` script via `yarn test`.</summary>
/// <param name="args" required="false" example="&quot;--verbose&quot;">Additional arguments forwarded after `--`.</param>
[<export>] let test (context: ActionContext) (args: string option) =
  let command =
    ""
    |> _helpers.append_part "test --"
    |> _helpers.append_part (_helpers.with_args args)

  [{ Command = "yarn"; Arguments = command; ErrorLevel = 0 }]

/// <summary>Runs an arbitrary yarn script (`yarn &lt;command&gt;`).</summary>
/// <param name="command" required="false" example="&quot;build-prod&quot;">Command to run.</param>
/// <param name="args" required="false" example="&quot;build-prod&quot;">Additional arguments forwarded after `--`.</param>
[<export>] let run (context: ActionContext) (command: string) (args: string option) =
  let operation =
    ""
    |> _helpers.append_part command
    |> _helpers.append_part "--"
    |> _helpers.append_part (_helpers.with_args args)

  [{ Command = "yarn"; Arguments = operation; ErrorLevel = 0 }]

// Exported functions and Terrabuild execution flags.
{
  [nameof defaults] = [Default]
  [nameof dispatch] = [Dispatch; Never]
  [nameof install] = [Local]
  [nameof build] = [Remote]
  [nameof test] = [Remote]
  [nameof run] = [Local]
}
