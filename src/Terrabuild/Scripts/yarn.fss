/// <summary>Provides build/test helpers for projects using **yarn** and a `package.json`. Defaults outputs to `dist/**` and infers dependencies from `package.json` to link Terrabuild graphs.</summary>
type Package = { name: string; dependencies: string map option; devDependencies: string map option }

let append_part part acc =
  match (part, acc) with
  | ("", _) -> acc
  | (_, "") -> part
  | _ -> $"{acc} {part}"

let map_true flag value =
  match value with
  | Some true -> flag
  | _ -> ""

let map_false flag value =
  match value with
  | Some true -> ""
  | _ -> flag

let with_args args =
  args |> Option.defaultValue ""

let load_package directory =
  let package_file = Fs.combinePath directory "package.json"
  let content = Fs.readText package_file |> Option.defaultValue ""
  Json.deserialize (typeof Package) content

let collect_local_dependency acc key value =
  match Regex.matchGroups "^file:(.*)$" value with
  | Some [project] -> project :: acc
  | _ -> acc

let collect_local_dependencies map =
  Map.fold collect_local_dependency [] map

let dependencies_from (package: Package) =
  let dependencies =
    package.dependencies
    |> Option.map collect_local_dependencies
    |> Option.defaultValue []

  let dev_dependencies =
    package.devDependencies
    |> Option.map collect_local_dependencies
    |> Option.defaultValue []

  List.append dependencies dev_dependencies
  |> List.distinct

/// <summary>Infers project metadata from `package.json` (dependencies and default outputs).</summary>
/// <param name="ignores" required="false" example="[ &quot;node_modules/**&quot; ]"></param>
/// <param name="outputs" required="false" example="[ &quot;dist/**&quot; ]"></param>
[<export>] let defaults (context: { Directory: string }) =
  let dependencies =
    context.Directory
    |> load_package
    |> Option.map dependencies_from
    |> Option.defaultValue []

  { Id = None; Outputs = ["dist/**"]; Dependencies = dependencies }

/// <summary>Runs an arbitrary yarn command (Terrabuild action name is forwarded to `yarn`).</summary>
/// <param name="args" required="false" example="&quot;--port=1337&quot;">Additional arguments appended after the yarn command.</param>
[<export>] let dispatch (context: { Command: string }) (args: string option) =
  let command =
    ""
    |> append_part context.Command
    |> append_part "--"
    |> append_part (with_args args)

  [{ Command = "yarn"; Arguments = command; ErrorLevel = 0 }]

/// <summary>Installs packages with `yarn install`, optionally updating the lockfile or ignoring engines.</summary>
/// <param name="update" required="false" example="true">Allow lockfile updates (omit to enforce frozen lockfile).</param>
/// <param name="ignore-engines" required="false" example="true">Adds `--ignore-engines`.</param>
/// <param name="args" required="false" example="&quot;--verbose&quot;">Additional arguments for `yarn install`.</param>
[<export>] let install (context: { Command: string }) (update: bool option) (ignoreEngines: bool option) (args: string option) =
  let update = update |> map_false "--frozen-lockfile"
  let ignore_engines = ignoreEngines |> map_true "--ignore-engines"
  let args = with_args args

  let command =
    ""
    |> append_part "install"
    |> append_part update
    |> append_part ignore_engines
    |> append_part args

  [{ Command = "yarn"; Arguments = command; ErrorLevel = 0 }]

/// <summary>Runs the `build` script via `yarn build`.</summary>
/// <param name="args" required="false" example="&quot;--verbose&quot;">Additional arguments forwarded after `--`.</param>
[<export>] let build (context: { Command: string }) (args: string option) =
  let command =
    ""
    |> append_part "build --"
    |> append_part (with_args args)

  [{ Command = "yarn"; Arguments = command; ErrorLevel = 0 }]

/// <summary>Runs the `test` script via `yarn test`.</summary>
/// <param name="args" required="false" example="&quot;--verbose&quot;">Additional arguments forwarded after `--`.</param>
[<export>] let test (context: { Command: string }) (args: string option) =
  let command =
    ""
    |> append_part "test --"
    |> append_part (with_args args)

  [{ Command = "yarn"; Arguments = command; ErrorLevel = 0 }]

/// <summary>Runs an arbitrary yarn script (`yarn &lt;command&gt;`).</summary>
/// <param name="command" required="false" example="&quot;build-prod&quot;">Command to run.</param>
/// <param name="args" required="false" example="&quot;build-prod&quot;">Additional arguments forwarded after `--`.</param>
[<export>] let run (context: { Command: string }) (command: string) (args: string option) =
  let operation =
    ""
    |> append_part command
    |> append_part "--"
    |> append_part (with_args args)

  [{ Command = "yarn"; Arguments = operation; ErrorLevel = 0 }]


type ExportFlag =
  | Dispatch
  | Default
  | Batchable
  | Never
  | Local
  | External
  | Remote

{
  [nameof defaults] = [Default]
  [nameof dispatch] = [Dispatch; Never]
  [nameof install] = [Local]
  [nameof build] = [Remote]
  [nameof test] = [Remote]
  [nameof run] = [Local]
}
